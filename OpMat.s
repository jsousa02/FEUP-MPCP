.text
.global OpMat
.type OpMat, "function"

OpMat:	STP 	X29, X30, [SP, -16]!
		MOV 	W6, 0

CICLO:	LDRB 	W9, [X2] // OPS
		CMP 	W9, 88 // X
		B.EQ 	FIM
		CMP 	W9, 76 // L
		B.EQ 	L
		CMP 	W9, 67 // C
		B.EQ 	C
		CMP		W9, 80 // P
		B.EQ	P
		CMP		W9, 66 // B
		B.EQ	B
		CMP 	W9, 79 // 0
		B.EQ	O
		B		FIM

O:		ADD		W6, W6, 1 // CONTAR QUANTAS VEZES UMA INSTRUÇÃO DESTE TIPO FOI EXECUTADA
		ADD		X2, X2, 1 // ENDEREÇO DO VALOR A PROCURAR
		MOV		X4, X2
		ADD		X2, X2, 1 // ENDEREÇO DA INSTRUÇÃO SEGUINTE
		STP 	X0, X1, [SP, -16]! // GUARDAR X0 E X1
		STP		X2, X3, [SP, -16]! // GUARDAR X2 E X3
		STR		W6, [SP, -16]! // GUARDAR W6
		MUL		W2, W0, W1 // TAMANHO DO VETOR
		MOV		X1, X3 // ENDEREÇO DO VETOR
		LDRB	W0, [X4] // VALOR A PROCURAR
		BL 		ocorr
		MOV 	W5, W0 // COLOCAR RESULTADO DA SUBROTINA EM W5
		LDR		W6, [SP], 16 // RECUPERAR W6
		LDP		X2, X3, [SP], 16 // RECUPERAR X2 E X3
		LDP		X0, X1, [SP], 16 // RECUPERAR X0 E X1
		B 		CICLO

B:		MOV 	W15, 0 // VALOR 0
		MOV 	W10, 255 // VALOR 255
		MOV 	W11, 0 // LINHAS
		MOV		W12, 0 // COLUNAS
		ADD		X2, X2, 1
OUTER:	CMP		W11, W1 // COMPARAR COM Nº DE LINHAS
		B.EQ	CICLO
		ADD		W11, W11, 1
		MOV		W12, 0 // REPOR NUMERO DA COLUNA
INNER:	CMP		W12, W0 // COMPARAR COM NUMERO DE COLUNAS
		B.EQ	OUTER
		SUB		W9, W11, 1
		MADD	W13, W9, W0, W12 // OBTER POSIÇÃO NA MATRIZ
		ADD		W12, W12, 1
		LDRB	W14, [X3, X13] // VALOR NA POSIÇAO DA MATRIZ
		CMP 	W14, 127
		B.LE	ZERO
		STRB	W10, [X3, X13] // ALTERAR O VALOR PARA 255
		B		INNER
ZERO:	STRB	W15, [X3, X13] // ALTERAR O VALOR PARA 0
		B 		INNER

P:		ADD		X2, X2, 1
		LDRB	W11, [X2] // COLUNA
		ADD 	X2, X2, 1
		LDRB 	W12, [X2] // LINHA
		ADD 	X2, X2, 1
		LDRB 	W13, [X2] // VALOR
		MADD	W14, W12, W0, W11 // POSIÇÃO NA MATRIZ
		STRB	W13, [X3, X14] // ALTERAR VALOR NA POSIÇAO DA MATRIZ
		ADD		X2, X2, 1
		B		CICLO

C:		MOV 	W14, 0 // LINHA ATUAL
		ADD		X2, X2, 1
		LDRB	W11, [X2] // COLUNA
		ADD		X2, X2, 1
		LDRB	W12, [X2] // VALOR
		ADD 	X2, X2, 1
AUXC:	CMP		W1, W14
		B.EQ 	CICLO
		MADD	W13, W14, W0, W11 // OBTER POSIÇÃO
		STRB	W12, [X3, X13] // ALTERAR VALORES NA COLUNA
		ADD		W14, W14, 1
		B		AUXC

L:		MOV 	W14, 0 //COLUNA ATUAL
		ADD		X2, X2, 1
		LDRB	W11, [X2] // LINHA
		ADD 	X2, X2, 1
		LDRB	W12, [X2] // VALOR
		ADD		X2, X2, 1
AUXL:	CMP		W0, W14
		B.EQ	CICLO
		MADD	W13, W11, W0, W14 // POSIÇÃO NO VETOR
		STRB 	W12, [X3, X13] // ALTERAR VALORES NA LINHA
		ADD 	W14, W14, 1
		B 		AUXL

FIM:	CMP 	W6, 0 // SE W6 = 0, ENTAO NENHUMA INSTRUÇÃO DO TIPO 'O' FOI EXECUTADA
		B.NE	AUXFIM
		MOV		W0, -1
		LDP		X29, X30, [SP], 16 // RECUPERAR X29 E X30
		RET
AUXFIM:	MOV		W0, W5 // MOVER O VALOR DA SUBROTINA OCORR PARA W0
		LDP		X29, X30, [SP], 16 // RECUPERAR X29 E X30
		RET
